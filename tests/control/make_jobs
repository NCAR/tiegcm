#!/bin/tcsh
#
# Make job scripts for control (seasonal for solar min,max) test runs, by calling mkjobscript.
# This includes #BSUB lsf resources for the NCAR yellowstone machine.
# This will also make empty *_out directories to contain stdout files from each case run.
#
# $modeldir: model root directory (mkjobscript is in the scripts directory).
# $template: text of the job script starting after user variable sets (also in scripts directory)
#
set model       = tiegcm
set version     = ${model}_trunk                # only used to set execdir below
set modeldir   = ../../                         # model root dir with scripts directory
set mkjobscript = $modeldir/scripts/mkjobscript
set template    = $modeldir/scripts/${model}.job.template
#
# execdir = Directory in which to build and execute the model.
# res = either 5.0 (for 5-degree resolution), or 2.5 (for 2.5-degree resolution)
#
set execdir  = /glade/scratch/$user/$version/control/${model}-ys
set res      = 5.0
#
# queue, nproc, and wc are requested lsf batch system resources 
# (will be specified with #BSUB in the output job scripts)
#
set queue    = premium # batch queue
set nproc    = 16      # number of MPI tasks
set wc       = 0:30    # wallclock limit hours:minutes

set cases = (decsol_smax decsol_smin junsol_smax junsol_smin mareqx_smax mareqx_smin sepeqx_smax sepeqx_smin)

foreach case ($cases)

if (! -d ${case}_out) mkdir ${case}_out

$mkjobscript \
   -fileout="${model}_${case}.job" \
   -template=$template \
   -model="$model" \
   -modeldir="$modeldir" \
   -execdir="$execdir" \
   -input="${model}_${case}.inp" \
   -output="${case}_out/${model}_${case}.out" \
   -modelres="$res" \
   -runscript="run_${case}.lsf" \
   -bsub_jobname="${case}" \
   -bsub_queue="$queue" \
   -bsub_nproc="$nproc" \
   -bsub_wc="$wc"

end
