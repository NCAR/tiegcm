#!/bin/tcsh
#
# Make namelist input files for control runs (seasonal with smin,smax conditions).
# This calls mknamelist, which is in the scripts directory of the model root directory.
#
# You may need to change $modeldir below so it can find the mknamelist script, or you can
# simply copy the script from the model scripts directory, and set mknamelist = ./mknamelist.
#
# You may want to tweak $version to get the source and output file names you want,
# or you can set the source file and output files separately below.
#
set model      = tiegcm
set version    = ${model}_trunk                 # for source and output file names
set modeldir   = ../../                         # model root dir with scripts directory
set mknamelist = $modeldir/scripts/mknamelist   # path to mknamelist
set modelres   = 2.5                            # either 5.0 or 2.5 degree resolution
#
# Set solar min and solar max cases:
#
set smax = "-POWER=39. -CTPOTEN=60. -F107=200. -F107A=200."
set smin = "-POWER=18. -CTPOTEN=30. -F107=70. -F107A=70."
#
# Case names for both equinoxes and solstices, each with smin and smax.
# Case names are used with version to set source and output file names.
#
set cases = (decsol_smax decsol_smin junsol_smax junsol_smin mareqx_smax mareqx_smin sepeqx_smax sepeqx_smin)

foreach case ($cases)
  if ($case == "decsol_smax" || $case == "decsol_smin") then
    set source_start = "355,0,0"
    set start_day = "355"
    set start = "355,0,0"
    set stop  = "360,0,0"
    set secstart = "355,1,0"
    set secstop = "360,0,0"
  else if ($case == "junsol_smax" || $case == "junsol_smin") then
    set source_start = "172,0,0"
    set start_day = "172"
    set start = "172,0,0"
    set stop  = "177,0,0"
    set secstart = "172,1,0"
    set secstop = "177,0,0"
  else if ($case == "mareqx_smax" || $case == "mareqx_smin") then
    set source_start = "80,0,0"
    set start_day = "80"
    set start = "80,0,0"
    set stop  = "85,0,0"
    set secstart = "80,1,0"
    set secstop = "85,0,0"
  else if ($case == "sepeqx_smax" || $case == "sepeqx_smin") then
    set source_start = "264,0,0"
    set start_day = "264"
    set start = "264,0,0"
    set stop  = "269,0,0"
    set secstart = "264,1,0"
    set secstop = "269,0,0"
  endif

  if ($case =~ *smax) then
    set solar = "$smax"
  else if ($case =~ *smin) then
    set solar = "$smin"
  else
    echo "Could not set solar for case $case"
    exit
  endif
#
  set source = "'$TGCMDATA/$version/${version}.pcntr_${case}.nc'"
  if ($modelres == "2.5") then
    set source = "'$TGCMDATA/${version}_dres/${version}.pcntr_${case}.nc'"
  endif

  $mknamelist -fileout=${model}_${case}.inp -modelres=$modelres \
    -LABEL="'$model $case control equilibration run'" \
    -START_DAY=$start_day \
    -SOURCE=$source \
    -SOURCE_START=$source_start \
    -START=$start -STOP=$stop -HIST="1,0,0" \
    -OUTPUT="'${version}.pcntr_${case}_001.nc'" \
    -SECSTART=$secstart -SECSTOP=$secstop -SECHIST="0,1,0" \
    -SECOUT="'${version}.scntr_${case}_001.nc','to','${version}.scntr_${case}_005.nc','by','1'" \
    -MXHIST_SECH=24 \
    -SECFLDS0='TN','UN','VN','O2','O1','N2','NO','N4S','HE','NE','TE','TI', \
    -SECFLDS1='TEC','O2P','OMEGA','POTEN','UI_ExB','VI_ExB','WI_ExB','DEN', \
    -SECFLDS2='QJOULE','Z','ZG' \
    $solar
end
