#
# Set up 4 jobs, including job scripts and namelist input files, for 4 seasons,
#   with heelis potential model (constant ctpoten, power, f107)
# To use, copy this script to a working directory with model root dir $modeldir,
#   and execute by typing "setup_seasons_heelis".
#
#   mareqx: March equinox (day 80)
#   junsol: June solstice (day 172)
#   sepeqx: September solstice (day 264)
#   decsol: December solstice (day 355)
#
# Set path to util scripts:
#
set modeldir   = tiegcm_trunk                  # model root dir
set utildir    = $modeldir/scripts             # for job script
set mknamelist = $modeldir/scripts/mknamelist  # mknamelist script
set mkjob      = $modeldir/scripts/mkjob       # mkjob command
#set system     = ibm
 set system     = linux
set template   = $modeldir/scripts/job_${system}.txt
set bsub_queue = 'debug'
set bsub_nproc = 8
#
# Set some defaults:
#
set cases = (mareqx junsol sepeqx decsol) # Will loop through these cases
set model = tiegcm                # For namelist and job scripts
set potential_model = "'HEELIS'"  # For namelist
set potential_name  = "heelis"    # For output file names
#
# Make namelist input files:
#
set TGCMDATA = '$TGCMDATA'
set source = ( "'$TGCMDATA/TGCM.tiegcm1.93.pcntr_eqnx_smed.nc'" \
               "'$TGCMDATA/TGCM.tiegcm1.93.pcntr_jsol_smed.nc'" \
               "'$TGCMDATA/TGCM.tiegcm1.93.pcntr_eqnx_smed.nc'" \
               "'$TGCMDATA/TGCM.tiegcm1.93.pcntr_dsol_smed.nc'" )
set start_day  = (80 172 264 355)
set source_start = (80 172 80  355)
set stop_day  = (81 173 265 356)
@ i = 1
foreach case ($cases)
  set output = "'${model}.p_${case}_${potential_name}_001.nc'"
  set secout = "'${model}.s_${case}_${potential_name}_001.nc'"
  $mknamelist -model=$model -fileout=${model}_${case}_${potential_name}.inp \
    -START_DAY=$start_day[$i] \
    -SOURCE=$source[$i] -SOURCE_START="$source_start[$i],0,0" \
    -START="$start_day[$i],0,0" -STOP="$stop_day[$i],0,0" -HIST="1,0,0" \
    -OUTPUT=$output \
    -SECSTART="$start_day[$i],1,0" -SECSTOP="$stop_day[$i],0,0" -SECHIST="0,1,0" \
    -SECOUT=$secout -POTENTIAL_MODEL=$potential_model
  @ i++
end
#
# Make job scripts:
#
set modelres = 5.0
foreach case ($cases)
  $mkjob -model=$model -fileout=${model}_${case}_${potential_name}.job \
    -system=$system -utildir=$utildir \
    -bsub_queue=$bsub_queue -bsub_nproc=$bsub_nproc \
    -template=$template \
    -modeldir="$modeldir" \
    -input="'${model}_${case}_${potential_name}.inp'" \
    -output="'${model}_${case}_${potential_name}.out'" \
    -modelres=$modelres
end
