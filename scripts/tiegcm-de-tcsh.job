#!/bin/tcsh
#
# Job script to build and execute TIEGCM on the NCAR supercomputer derecho,
# or similar system running PBS (Load Sharing Facility)
#
# This script should be executed on the derecho command line.
# It will compile in execdir on the interactive login node, then create and
# submit an PBS script based on the #PBS resources specified below.
#
# User should set shell variables and #PBS settings below:
#
#   modeldir:  Root directory to model source (may be an SVN working dir)
#   execdir:   Directory in which to build and execute (will be created if necessary)
#   tgcmdata:  Directory in which startup history and data files are accessed.
#              (If tgcmdata is not set, the model will use env var TGCMDATA)
#   utildir:   Directory containing supporting scripts (default $modeldir/scripts)
#   input:     Namelist input file for the chosen model resolution
#   output:    Stdout file from model execution (will be created)
#   horires:   Model resolution (5, 2.5, 1.25 or 0.625 degrees)
#   vertres:   Model resolution (0.5, 0.25, 0.125 or 0.0625 degrees)
#   zitop:     Pressure level at model upper boundary
#   nres_grid: Number of levels for multigrid solver (magnetic grid)
#   coupling:  Whether to couple with GAMERA via MPI
#   make:      Build file with platform-specific compile parameters (in scripts dir)
#   debug:     If TRUE, build and execute a "debug" run (debug compiler flags are set)
#   execute:   If TRUE, execute the model (build only if execute is FALSE)
#   runscript: PBS script with run commands (submitted with qsub from execdir)
#
# To switch to 2.5x0.25-deg resolution, set horires below to 2.5 and vertres to 0.25,
# and change execdir, tgcmdata and namelist input if necessary.
# Also reset number of processors accordingly below (#PBS -l).
#
set modeldir  = /glade/campaign/hao/itmodel/tiegcm3.0/tiegcm
set execdir   = /glade/derecho/scratch/$user/tiegcm
set tgcmdata  = /glade/p/hao/tgcm/data
set utildir   = $modeldir/scripts
set input     = $utildir/tiegcm_default.inp
set output    = tiegcm.out
set horires   = 2.5
set vertres   = 0.25
set zitop     = 7
set mres      = 2
set coupling  = FALSE
set make      = Make.intel_de
set debug     = FALSE
set execute   = FALSE
set runscript = run.pbs

set project_code    = P28100036
set queue           = main
set job_priority    = regular
set nnodes          = 1
set ncores_per_node = 128
set walltime        = "12:00:00"
@ nprocs            = $nnodes * $ncores_per_node

set execdir   = `perl $utildir/abspath $execdir`
set runscript = `perl $utildir/abspath $runscript`
if (! -d $execdir) mkdir -p $execdir
#
#- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
#                               End user settings
#                        Shell Script for TIEGCM Linux job
#- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
#
# Necessary modules on derecho
module load mkl
module swap netcdf netcdf-mpi
module load esmf
#
set mycwd = `pwd`
echo "" ; echo "${0}:"
echo "  Begin execution at `date`"
echo "  Current working directory: $mycwd"
echo "  System: `uname -a`"
echo ""
#
# Verify directories and make_machine file (make execdir if necessary).
# Get absolute path for dirs that are accessed from the execdir.
#
if (! -d $modeldir) then
  echo ">>> Cannot find model directory $modeldir <<<"
  exit 1
endif
#
# Executable file name:
set modelexe = tiegcm.exe

if (! -d $utildir) then
  echo ">>> Cannot find model scripts directory $utildir <<<"
  exit 1
endif
set utildir = `perl $utildir/abspath $utildir`

set srcdir = $modeldir/src
if (! -d $srcdir) then
  echo ">>> Cannot find model source directory $srcdir <<<"
  exit 1
endif
set srcdir = `perl $utildir/abspath $srcdir`
#
# Set data directory:
#   If tgcmdata is set above, use it to set (override) env var TGCMDATA
#   If tgcmdata not set above, use env var TGCMDATA
#   If neither are set, print a warning and set both to cwd.
#   Finally, print warning if the directory does not exist.
#
if ($?tgcmdata) then
  set tgcmdata = `perl $utildir/abspath $tgcmdata`
  setenv TGCMDATA $tgcmdata
  echo "Set env var TGCMDATA = $TGCMDATA"
else
  if ($?TGCMDATA) then
    set tgcmdata = $TGCMDATA
    echo "Set tgcmdata = $TGCMDATA"
  else
    echo "WARNING: TGCMDATA is not set - using cwd"
    set tgcmdata = .
    setenv TGCMDATA $tgcmdata
  endif
endif
if (! -d $tgcmdata) echo ">>> Cannot find data directory $tgcmdata"

if ($horires != 5 && $horires != 2.5 && $horires != 1.25 && $horires != 0.625) then
  echo ">>> Unknown model horizontal resolution $horires <<<"
  exit 1
endif
if ($vertres != 0.5 && $vertres != 0.25 && $vertres != 0.125 && $vertres != 0.0625) then
  echo ">>> Unknown model vertical resolution $vertres <<<"
  exit 1
endif

if ($mres == 2) then
  set nres_grid = 5
else if ($mres == 1) then
  set nres_grid = 6
else if ($mres == 0.5) then
  set nres_grid = 7
else
  echo ">>> Unsupported magnetic resolution $mres <<<"
  exit 1
endif
#
# Copy make files to execdir if necessary:
#
if (! -f $execdir/$make)     cp $utildir/$make $execdir
if (! -f $execdir/Makefile)  cp $utildir/Makefile $execdir
if (! -f $execdir/mkdepends) cp $utildir/mkdepends $execdir
#
# Make default namelist input file if not provided by user:
#
if (! -f $input) then
  echo ">>> Cannot find namelist input file $input <<<"
  exit 1
endif

set model  = $execdir/$modelexe
set input  = `perl $utildir/abspath $input`
set output = `perl $utildir/abspath $output`
set util   = `perl $utildir/abspath $utildir`
set mklogs = $util/mklogs         # Nov, 2015: mklogs rewritten in python
set rmbinchars = $util/rmbinchars # Nov, 2015: remove non-ascii chars from stdout files
#
# Report to stdout:
#
if (`where svnversion` == "") then
  set svn_revision = `svnversion $modeldir`
else
  set svn_revision = "[none]"
endif
#set svn_revision = "tiegcm2.5" # for svn tag instead of revision number

echo -n "  Model directory:   $modeldir"
echo " (SVN revision $svn_revision)"
echo "  Exec directory:    $execdir"
echo "  Source directory:  $srcdir"
echo "  Data directory:    $tgcmdata"
echo "  Make machine file: $make"
echo "  Namelist input:    $input"
echo "  Stdout Output:     $output"
echo "  Model resolution:  $horires x $vertres"
echo "  Coupling:          $coupling"
echo "  Debug:             $debug"
#
# If coupling flag has changed from last gmake, clean execdir
# and reset coupling file:
#
if (-f $execdir/coupling) then
  set lastcoupling = `cat $execdir/coupling`
  if ($lastcoupling != $coupling) then
    echo "Clean execdir $execdir because coupling flag switched from $lastcoupling to $coupling"
    set mycwd = `pwd` ; cd $execdir ; gmake clean ; cd $mycwd
    echo $coupling >! $execdir/coupling
  endif
else
  echo $coupling > $execdir/coupling
  echo "Created file coupling with coupling flag = $coupling"
endif
#
# If debug flag has changed from last gmake, clean execdir
# and reset debug file:
#
if (-f $execdir/debug) then
  set lastdebug = `cat $execdir/debug`
  if ($lastdebug != $debug) then
    echo "Clean execdir $execdir because debug flag switched from $lastdebug to $debug"
    set mycwd = `pwd` ; cd $execdir ; gmake clean ; cd $mycwd
    echo $debug >! $execdir/debug
  endif
else
  echo $debug >! $execdir/debug
  echo "Created file debug with debug flag = $debug"
endif
#
# Copy defs header file to execdir, if necessary, according to
# requested resolution. This should seamlessly switch between
# resolutions according to $horires and $vertres.
#
set defs = defs.h

cat << EOF > $defs
#define DLAT $horires
#define DLON $horires
#define GLON1 -180
#define DLEV $vertres
#define ZIBOT -7
#define ZITOP $zitop
#define NRES_GRID $nres_grid
EOF

if (-f $execdir/defs.h) then
  cmp -s $execdir/defs.h $defs
  if ($status != 0) then # files differ -> switch resolutions
    echo "Switching defs.h for model resolution $horires x $vertres"
    mycwd=`pwd` ; cd $execdir ; gmake clean ; cd $mycwd
    cp $defs $execdir/defs.h
  else
    echo "defs.h already set for model resolution $horires x $vertres"
  endif
else # defs.h does not exist in execdir -> copy appropriate defs file
  echo "Copying $defs to $execdir/defs.h for resolution $horires x $vertres"
  cp $defs $execdir/defs.h
endif
#
# cd to execdir and run make:
#
cd $execdir
if ($status != 0) then
  echo ">>> Cannot cd to execdir $execdir"
  exit 1
endif
echo ""
echo "Begin building $model in `pwd`"
#
# Build Make.env file in exec dir, containing needed env vars for Makefile:
#
cat << EOF >! Make.env
MAKE_MACHINE  = $make
DIRS          = . $srcdir
EXECNAME      = $model
NAMELIST      = $input
OUTPUT        = $output
COUPLING      = $coupling
DEBUG         = $debug
SVN_REVISION  = $svn_revision
EOF
#
# Build the model:
gmake -j8 all
if ($status != 0) then
  echo ">>> Error return from gmake all"
  exit 1
endif
#
# Set PBS resource usage (create the runscript in execdir):
# (run commands are appended to this script below)
#
# Set data directory in PBS script:
#
# MPI/PBS job: append mpirun.command command to PBS script
# (it has #PBSs from above)
#
cat << EOF >! $runscript
#!/bin/tcsh
#PBS -N tiegcm
#PBS -A $project_code
#PBS -q $queue
#PBS -j oe
#PBS -l job_priority=$job_priority
#PBS -l select=${nnodes}:ncpus=${ncores_per_node}:mpiprocs=${ncores_per_node}
#PBS -l walltime=$walltime

setenv TGCMDATA $tgcmdata

# Execute:
cd $execdir
`cat mpirun.command` -np $nprocs $model $input >&! $output

# Save stdout:
$rmbinchars $output # remove any non-ascii chars in stdout file
$mklogs $output     # break stdout into per-task log files
cd `dirname $output`

# Make tar file of task log files:
tar -cf $output.tar *task*.out
echo "Saved stdout tar file $output.tar"
rm *task*.out
EOF
#
if ($execute == "TRUE") then
  echo " "
  echo "Submitting PBS script $runscript for MPI run"
  qsub < $runscript
else
  echo "Am NOT executing the model because execute = $execute"
endif
#
exit 0
