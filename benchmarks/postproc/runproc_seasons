#!/bin/tcsh
#
# Make runs of post-processor to generate plots from seasonal benchmark runs:
# (instantaneous and time-dependent)
#
set proc = tgcmproc_f90/tgcmproc/tgcmproc        # python code to make multiple proc runs
set home = /glade/p/hao/tgcm/tiegcm_benchmarks   # base working directory

set resolutions = ( \
  '5.0' \
  '2.5' \
)

foreach res ($resolutions)

  if ($res == '5.0') then
    set exec = tgcmproc_f90/build/tgcmproc_sres_f90  # single-res processor executable
  else if ($res == '2.5') then
    set exec = tgcmproc_f90/build/tgcmproc_dres_f90  # double-res processor executable
  else
    echo ">>> Unknown resolution: $res"
    exit 
  endif

  set runs = ( \
    tiegcm_res${res}_decsol_smax \
    tiegcm_res${res}_decsol_smin \
    tiegcm_res${res}_junsol_smax \
    tiegcm_res${res}_junsol_smin \
    tiegcm_res${res}_mareqx_smax \
    tiegcm_res${res}_mareqx_smin \
    tiegcm_res${res}_sepeqx_smax \
    tiegcm_res${res}_sepeqx_smin \
  )

  set case   = tiegcm_res${res}_seasons
  set work   = $home/$case

  foreach run ($runs)
    echo "------------------ $run --------------------"
    set outdir  = $work/$run/proc
    set histdir = $work/$run/hist
#
# Instantaneous maps, slices, etc at last history: 
#
    $proc -submit yes -executable $exec -singleut=1 \
      -nlfile $outdir/${run}_singleut.inp -stdout $outdir/${run}_singleut.out \
      -flnm_ps $outdir/${run}_singleut.ps $histdir/\*sech\*.nc
    echo Completed singleut for run $run
#
# Time-dependent plots with day/ut on x-axis:
#
    $proc -submit yes -executable $exec -multiut=1 \
      -nlfile $outdir/${run}_multiut.inp -stdout $outdir/${run}_multiut.out \
      -flnm_ps $outdir/${run}_multiut.ps $histdir/\*sech\*.nc
    echo Completed multiut for run $run

  end # foreach run
end # foreach res

#---------------------------------------------------------------------------------------
